buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
    }
}

repositories {
    google()
    mavenCentral()
}

apply from: 'publish.gradle'
apply from: 'version.gradle'

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.android.library'

def wasSigned = false

def ext = rootProject.ext

// Setup signing values.
ext."signing.keyId" = ext.mavenSigningKeyId
ext."signing.secretKeyRingFile" = ext.mavenSecretKeyPath
ext."signing.password" = ext.mavenPublicKeyPassword

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 69
        versionName '5.0.3'
        group groupId
        version versionName
        buildConfigField 'String', "SDK_NAME", "\"appcenter.react-native\""
        buildConfigField 'String', 'VERSION_NAME', "\"${ext.versionName}\""

        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
        consumerProguardFiles 'proguard-rules.pro'
    }
}

task sourcesJar(type: Jar) {
    afterEvaluate {
        from android.sourceSets.main.java.srcDirs
        classifier = 'sources'
    }
}

publishing {
    repositories {
        maven {
            url = uri("${System.env.BUILD_DIR}")
        }
    }

    afterEvaluate { project ->
        publications {
            maven(MavenPublication) {
                from components.release

                artifact sourcesJar

                pom {
                    packaging = 'aar'
                    name = project.name
                    description = sdkDescription
                    url = siteUrl

                    // Set identifiers of assemble.
                    groupId = ext.groupId
                    artifactId = project.name

                    // Set license information.
                    licenses {
                        license {
                            name = licenseName
                            url = licenseSite
                        }
                    }

                    // Set information about developers.
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }

                    // Set information about connection with developers.
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

dependencies {
    api 'com.microsoft.appcenter:appcenter:5.0.6'
}

artifacts {
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications
}