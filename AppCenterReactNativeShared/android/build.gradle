buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: 'publish.gradle'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.github.dcendents.android-maven'

def wasSigned = false

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter {
            content {
                // https://youtrack.jetbrains.com/issue/IDEA-261387
                includeModule("org.jetbrains.trove4j", "trove4j")
            }
        }
    }

    def ext = rootProject.ext
    
    // Setup signing values.
    ext."signing.keyId" = ext.mavenSigningKeyId
    ext."signing.secretKeyRingFile" = ext.mavenSecretKeyPath
    ext."signing.password" = ext.mavenPublicKeyPassword

    android {
        compileSdkVersion 29
        buildToolsVersion '29.0.2'

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 29
            versionCode 61
            versionName '4.4.2'
            group groupId
            version versionName
            buildConfigField 'String', "SDK_NAME", "\"appcenter.react-native\""

            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    task sourcesJar(type: Jar) {
        afterEvaluate {
            from android.sourceSets.main.java.srcDirs
            classifier = 'sources'
        }
    }

    
    afterEvaluate {
        uploadArchives {
            repositories.mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> 
                    // workaround to prevent the script from failing because of double uploading of pom files
                    // related issue: https://github.com/gradle/gradle/issues/3003
                    if (!wasSigned) {
                        signing.signPom(deployment)
                        wasSigned = true
                    }
                }

                repository(url: ext.mavenRepoUrl) {
                    authentication(userName: ext.mavenUser, password: ext.mavenKey)
                }

                pom.project {

                    // Set base information about assemble.
                    packaging = 'aar'
                    name = project.name
                    description = sdkDescription
                    url = siteUrl

                    // Set identifiers of assemble.
                    groupId = ext.groupId
                    artifactId = project.name

                    // Set license information.
                    licenses {
                        license {
                            name = licenseName
                            url = licenseSite
                        }
                    }

                    // Set information about developers.
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }

                    // Set information about connection with developers.
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                }
            }
        }
    }
}

dependencies {
    api 'com.microsoft.appcenter:appcenter:4.4.2'
}

artifacts {
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}
