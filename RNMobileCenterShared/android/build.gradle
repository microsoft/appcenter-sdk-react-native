buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: 'publish.gradle'

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

allprojects {
    def ext = rootProject.ext

    android {
        compileSdkVersion 23
        buildToolsVersion '23.0.1'

        defaultConfig {
            minSdkVersion 16
            targetSdkVersion 22
            versionCode 13
            versionName "0.10.0"
            group groupId
            version versionName
            buildConfigField 'String', "SDK_NAME", "\"mobilecenter.react-native\""

            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    task sourcesJar(type: Jar) {
        afterEvaluate {
            from android.sourceSets.main.java.srcDirs
            classifier = 'sources'
        }
    }

    bintray {
        user = bintrayUser
        key = bintrayKey

        configurations = ['archives']

        publish = true

        pkg {
            repo = bintrayRepo
            name = "mobile-center-react-native"
            userOrg = bintrayUserOrg
            vcsUrl = gitUrl
            websiteUrl = siteUrl
            licenses = [licenseCode]
        }
    }

    install {
        repositories.mavenInstaller {
            pom {
                //noinspection GroovyAssignabilityCheck
                project {
                    packaging 'aar'

                    //noinspection GroovyAssignabilityCheck
                    name project.name
                    description = sdkDescription
                    url = siteUrl

                    licenses {
                        license {
                            //noinspection GroovyAssignabilityCheck
                            name licenseName
                            url licenseSite
                        }
                    }
                    developers {
                        developer {
                            id developerId
                            //noinspection GroovyAssignabilityCheck
                            name developerName
                            email developerEmail
                        }
                    }
                    scm {
                        connection gitUrl
                        developerConnection gitUrl
                        url siteUrl
                    }
                }
            }
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.microsoft.azure.mobile:mobile-center:0.12.0'
}

artifacts {
    archives sourcesJar
}
